# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

_name=dulwich
pkgname=python-dulwich
pkgver=0.19.9
pkgrel=1
pkgdesc='Pure-Python implementation of the Git file formats and protocols'
arch=('x86_64')
url=https://www.dulwich.io
license=('GPL')
depends=('python')
checkdepends=('python-urllib3')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('5e1e39555f594939a8aff1ca08b3bdf6c7efd4b941c2850760983a0197240974')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  python -m unittest dulwich.tests.test_suite
}

package() {
  cd $_name-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
