# Maintainer: Daniel M. Capella <polyzen@archlinux.org>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

_name=dulwich
pkgname=python-dulwich
pkgver=0.20.15
pkgrel=1
pkgdesc='Pure-Python implementation of the Git file formats and protocols'
arch=('x86_64')
url=https://www.dulwich.io
license=('GPL')
depends=('python-certifi' 'python-setuptools' 'python-urllib3')
source=("https://files.pythonhosted.org/packages/source/${_name::1}/$_name/$_name-$pkgver.tar.gz")
sha256sums=('fb1773373ec2af896031f8312af6962a1b8b0176a2de3fb3d84a84ec04498888')
b2sums=('8db19f84154dff0851fd16bc534d92b506c93a93d7180079bc9829a2c81bbb25b04b234770e62ddab82440973d0702b8523896a9db90d771d8cc915b6321cdaa')

build() {
  cd $_name-$pkgver
  python setup.py build
}

check() {
  cd $_name-$pkgver
  python -m unittest dulwich.tests.test_suite
}

package() {
  cd $_name-$pkgver
  export PYTHONHASHSEED=0
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build
}

# vim:set ts=2 sw=2 et:
